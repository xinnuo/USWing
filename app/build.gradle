apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

repositories {
    mavenCentral()
    flatDir { dir 'libs' }
}

android {
    compileSdkVersion versions.compileSdkVersion
    defaultConfig {
        applicationId "com.meida.uswing"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode 101
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        applicationVariants.all { variant ->
            if (variant.buildType.name == "release") {
                variant.getPackageApplicationProvider().get()
                        .outputScope
                        .apkDatas
                        .forEach { apkData ->
                    // def version = "v${versionName}(${versionCode})"
                    // def formatDate = new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("UTC"))
                    // def fileName = file.name.replace(".apk", "-${version}-${formatDate}.apk")
                    // def fileName = file.name.replace(".apk", "-v${versionName}.apk")
                    def fileName = "${applicationId}-v${versionName}.apk"
                    apkData.outputFileName = fileName
                }
            }
        }

        //支持 armeabi-v7a 和 x86 架构 CPU
        //处理.so文件时的重要法则:要么全部支持，要么都不支持
        ndk { abiFilters "armeabi-v7a", "x86" }

        //高德地图的apikey，融云的pkgname、key，极光推送的pkgname、key、channel
        manifestPlaceholders = [
                amap_apikey  : "2de5633cf3ebd2fddd17bd122a75ef6b",
                RONG_PKGNAME : applicationId,
                RONG_APPKEY  : "0vnjpoad03goz",
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "c4c51995512953459514bd4e",
                JPUSH_CHANNEL: "developer-default"
        ]
    }

    buildTypes {
        debug {
            //显示Log打印输出
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "String", "API_HOST", "\"http://39.108.168.136\""
            minifyEnabled false   //混淆编译
            zipAlignEnabled false //Zip代码压缩优化
        }
        release {
            //不显示Log打印输出
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "String", "API_HOST", "\"http://39.108.168.136\""
            minifyEnabled false  //混淆编译
            zipAlignEnabled true //Zip代码压缩优化
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file('../USWing.jks')
            storePassword "123456"
            keyAlias "USWing"
            keyPassword "123456"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //移除lint检查的error（程序在buid的时候，会执行lint检查）
    lintOptions {
        abortOnError false  // true--错误发生后停止gradle构建
        ignoreWarnings true // true--只报告error
        showAll true        // true--显示错误的所有发生位置，不截取
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main { jniLibs.srcDirs = ['libs'] }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //单引号''中的内容严格对应Java中的String，不会对$符号进行转义
    def support = ['appcompat-v7:', 'recyclerview-v7:', 'design:', 'support-v4:', 'exifinterface:']
    support.each { implementation 'com.android.support:' + it + versions.supportLibraryVersion }

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:' + versions.kotlinPlugin
    implementation 'com.android.support.constraint:constraint-layout:' + versions.constraintLayoutVersion
    implementation 'com.android.support:multidex:' + versions.multidexVersion
    testImplementation 'junit:junit:4.12'

    def anko = ['sdk25:', 'sdk25-listeners:', 'appcompat-v7:',
                'recyclerview-v7:', 'recyclerview-v7-listeners:',
                'cardview-v7:', 'design:', 'design-listeners:',
                'support-v4:']
    anko.each { implementation 'org.jetbrains.anko:anko-' + it + versions.ankoVersion }

    implementation project(':AMap')
    implementation project(':ijkplayer')
    implementation project(':okgo')
    implementation project(':RongCloudIMkit')
    def aar = ['FlycoDialog-1.3.2',
               'RollViewPager',
               'SuperText-1.1.1',
               'SVProgressHUD-1.0.6']
    aar.each { implementation(name: it, ext: 'aar') }

    //图片浏览
    implementation 'com.github.moyokoo:Diooto:1.23'
    //noinspection GradleDependency
    implementation 'me.panpf:sketch:2.6.2'
    //noinspection GradleDependency
    implementation 'me.panpf:sketch-gif:2.6.2'

    //图片加载
    implementation 'com.github.bumptech.glide:glide:' + versions.glideVersion
    kapt 'com.github.bumptech.glide:compiler:' + versions.glideVersion
    implementation "com.github.bumptech.glide:okhttp3-integration:" + versions.glideVersion

    //图片选择
    implementation('com.github.LuckSiege.PictureSelector:picture_library:v2.2.3') {
        exclude group: 'com.github.bumptech.glide', module: 'glide'
    }

    //极光推送
    implementation 'cn.jiguang.sdk:jpush:' + versions.jpushSdkVersion
    implementation 'cn.jiguang.sdk:jcore:' + versions.jpushCoreVersion

    //二维码扫描和生成
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'

    //在线支付
    implementation 'com.cuieney:rxpay-api:2.2.3'
    kapt 'com.cuieney:rxpay-compiler:2.1.1'

    implementation 'com.github.santalu:mask-edittext:1.0.1'
    implementation 'com.github.zcweng:switch-button:0.0.3@aar'
    implementation 'com.hyman:flowlayout-lib:1.1.2'
    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1'
    implementation 'com.loopeer.lib:shadow:0.0.4-beta3'
    implementation 'com.lqr.ninegridimageview:library:1.0.0'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'com.sunfusheng:GlideImageView:2.0.0'
    implementation 'com.yanzhenjie:permission:2.0.0-rc12'
    implementation 'com.weigan:loopView:0.1.2'
    implementation 'net.idik:slimadapter:2.1.2'
    implementation 'org.greenrobot:eventbus:3.1.1'
}
